CMAKE_MINIMUM_REQUIRED (VERSION 3.7...3.13)

PROJECT(MatrixCF LANGUAGES CXX)

# options
OPTION(MATRIXCF_BUILD_EXAMPLES OFF)
OPTION(MATRIXCF_BUILD_TESTS OFF)

# add MatrixCF library as h-only
ADD_LIBRARY(MatrixCF INTERFACE)
ADD_LIBRARY(MatrixCF::MatrixCF ALIAS MatrixCF)

# include folders
TARGET_INCLUDE_DIRECTORIES(MatrixCF INTERFACE include)
TARGET_INCLUDE_DIRECTORIES(MatrixCF INTERFACE vendor/EasyCL/include/EasyCL)

# found OpenCL
FIND_PACKAGE(OpenCL REQUIRED)

# link to OpenCL
TARGET_LINK_LIBRARIES(MatrixCF INTERFACE OpenCL::OpenCL)

# build an examples
IF(MATRIXCF_BUILD_EXAMPLES)
    CONFIGURE_FILE(matrix.cl examples/matrix.cl COPYONLY)
    MACRO(matrixcf_add_example EXAMPLENAME)
        ADD_EXECUTABLE(${EXAMPLENAME} ${ARGN})
        TARGET_LINK_LIBRARIES(${EXAMPLENAME} PRIVATE MatrixCF::MatrixCF)
        SET_TARGET_PROPERTIES(${EXAMPLENAME} PROPERTIES FOLDER examples)
    ENDMACRO()
    ADD_SUBDIRECTORY(examples)
ENDIF()

# build tests
IF(MATRIXCF_BUILD_TESTS)
    ADD_LIBRARY(Catch2 INTERFACE)
    TARGET_INCLUDE_DIRECTORIES(MatrixCF INTERFACE vendor/EasyCL/vendor/Catch2/single_include)
    enable_testing()

    CONFIGURE_FILE(matrix.cl tests/matrix.cl COPYONLY)
    MACRO(matrixcf_add_test TESTNAME)
        ADD_EXECUTABLE(${TESTNAME} ${ARGN})
        TARGET_LINK_LIBRARIES(${TESTNAME} PRIVATE Catch2)
        TARGET_LINK_LIBRARIES(${TESTNAME} PRIVATE MatrixCF::MatrixCF)
        SET_TARGET_PROPERTIES(${TESTNAME} PROPERTIES FOLDER tests)
    ENDMACRO()
    ADD_SUBDIRECTORY(tests)
ENDIF()